apply plugin: "maven"
apply plugin: "maven-publish"
apply plugin: "java"
apply plugin: "net.ltgt.apt"
apply plugin: "com.jfrog.bintray"

sourceCompatibility = JavaVersion.VERSION_1_7
targetCompatibility = JavaVersion.VERSION_1_7

dependencies {
    compile "com.squareup.retrofit2:retrofit:$RETROFIT_VERSION"
    compile "com.bluelinelabs:logansquare:$LOGANSQUARE_VERSION"

    testCompile "com.squareup.okhttp3:mockwebserver:$MOCKWEBSERVER_VERSION"
    testCompile "junit:junit:$JUNIT_VERSION"
    testCompile "org.assertj:assertj-core:$ASSERTJ_VERSION"
    testCompile "com.bluelinelabs:logansquare:$LOGANSQUARE_VERSION"
    testApt "com.bluelinelabs:logansquare-compiler:$LOGANSQUARE_VERSION"
}

// ------------------------------------------------------------------------------------------------
// Deployment Setup
//
// Releases are pushed to jcenter via Bintray, while snapshots are pushed to Sonatype OSS.
// This section defines the necessary tasks to push new releases and snapshots using Gradle tasks.
// ------------------------------------------------------------------------------------------------

version = VERSION_NAME

task copyPom(type: Copy) {
    from "build/publications/library"
    into "build/poms"
    include "pom-default.xml"
}

publish.dependsOn copyPom

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = "sources"
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = "javadoc"
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

publishing {
    publications {
        library(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            groupId GROUP_ID
            artifactId ARTIFACT_ID
            version VERSION_NAME
            pom.withXml {
                def root = asNode()
                root.appendNode("description", DESCRIPTION)
                root.appendNode("name", ARTIFACT_ID)
                root.appendNode("url", VCS_URL)
            }
        }
    }
}

project.configure(project) {
    if (project.version.endsWith("-SNAPSHOT")) {
        // Configure deployment of snapshot versions to Sonatype OSS
        project.publishing {
            repositories {
                maven {
                    name "snapshot"
                    credentials {
                        username project.ext.sonatypeUser
                        password project.ext.sonatypePass
                    }
                    url "https://oss.sonatype.org/content/repositories/snapshots"
                }
            }
        }

    } else {
        // Configure deployment of release versions to Bintray
        project.bintray {
            user = project.ext.bintrayUser
            key = project.ext.bintrayKey
            configurations = ["archives"]
            dryRun = false
            pkg {
                repo = "maven"
                name = "retrofit2-logansquare"
                userOrg = project.ext.bintrayUser
                licenses = ["Apache-2.0"]
                publish = true
                publicDownloadNumbers = true
                vcsUrl = VCS_URL
                version {
                    name = VERSION_NAME
                    desc = DESCRIPTION
                }
            }
        }
    }
}
