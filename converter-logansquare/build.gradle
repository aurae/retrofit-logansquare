apply plugin: "maven"
apply plugin: "maven-publish"
apply plugin: "java"
apply plugin: "net.ltgt.apt"
apply plugin: "com.jfrog.bintray"

sourceCompatibility = JavaVersion.VERSION_1_7
targetCompatibility = JavaVersion.VERSION_1_7

dependencies {
    compile "com.squareup.retrofit2:retrofit:$RETROFIT_VERSION"
    compile "com.bluelinelabs:logansquare:$LOGANSQUARE_VERSION"

    testCompile "com.squareup.okhttp3:mockwebserver:$MOCKWEBSERVER_VERSION"
    testCompile "junit:junit:$JUNIT_VERSION"
    testCompile "org.assertj:assertj-core:$ASSERTJ_VERSION"
    testCompile "com.bluelinelabs:logansquare:$LOGANSQUARE_VERSION"
    testApt "com.bluelinelabs:logansquare-compiler:$LOGANSQUARE_VERSION"
}

version = VERSION_NAME

bintray {
    user = project.ext.bintrayUser
    key = project.ext.bintrayApiKey
    configurations = ["archives"]
    pkg {
        repo = "maven"
        name = "retrofit2-logansquare"
        userOrg = properties.get("BINTRAY_USER")
        licenses = ["Apache-2.0"]
        publish = true
        publicDownloadNumbers = true
        vcsUrl = "https://github.com/mannodermaus/retrofit-logansquare"
        version {
            name = VERSION_NAME
            desc = DESCRIPTION
        }
    }
}

publishing {
    publications {
        MyPublication(MavenPublication) {
            from components.java
            groupId GROUP_ID
            artifactId ARTIFACT_ID
            version VERSION_NAME
        }
    }
}

task copyPom(type: Copy) {
    from "build/publications/MyPublication"
    into "build/poms"
    include "pom-default.xml"
}

publish.dependsOn copyPom

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = "sources"
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = "javadoc"
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}
